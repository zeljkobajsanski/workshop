<?xml version="1.0" encoding="utf-8"?>
<mx:ComboBox xmlns:mx="http://www.adobe.com/2006/mxml" rowCount="20">

	<mx:Script>
      <![CDATA[
		 
         private var _selectedValue:int;
         private var _dataField:String = "data";
         private var bSelectedValueSet:Boolean = false;
         private var bDataProviderSet:Boolean = false;
         
         // Override committ, this may be called repeatedly
         override protected function commitProperties():void
         {
            // invoke ComboBox version
            super.commitProperties();

            // If value set and have dataProvider
            if (bSelectedValueSet && bDataProviderSet)
            {
               // Set flag to false so code won't be called until selectedValue is set again
               bSelectedValueSet=false;
               
               // Set to index to -1
             	this.selectedIndex = -1
               
               // Loop through dataProvider
	               for (var i:int=0;i<this.dataProvider.length;i++)
	               {
	               		// some ugly hack in case there is an empty null element in the dataProvider
	               		if(this.dataProvider[i] != null){
							// Get this item's data
							var item:int = this.dataProvider[i][_dataField];
							   
							// Check if is selectedValue
							if(item == _selectedValue)
							{
								// Yes, set selectedIndex
								this.selectedIndex = i;
								break;
							}
		                }
	               }
            }
         }

         // Trap dataProvider being set
         override public function set dataProvider(o:Object):void
         {
            // invoke ComboBox version
            super.dataProvider = o;

            // This may get called before dataProvider is set, so make sure not null and has entries
            if (o!=null && o.length)
            {
               // Got it, set flag
               bDataProviderSet = true;
            }
         }

         // set for selectedValue
		 [Bindable]
         public function set selectedValue(s:int):void
         {
            // Set flag
            bSelectedValueSet = true;
            // Save value
            _selectedValue = s;
            // Invalidate to force commit
            invalidateProperties();
         }

		 public function get selectedValue() : int {
			 return _selectedValue;
		 }
         
         public function set dataField(s:String):void
         {
         	// SET datafield
         	_dataField = s;
         }
      ]]>
   </mx:Script>
   
</mx:ComboBox>
